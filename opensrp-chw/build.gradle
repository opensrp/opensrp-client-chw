buildscript {
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'org.apache.commons:commons-lang3:3.10'
        classpath 'com.android.tools.build:gradle:4.0.2'
        classpath 'com.google.gms:google-services:4.3.4'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.4.1'
    }
}

allprojects {
    repositories {
        maven { url 'https://maven.google.com' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven {
            def githubProperties = new Properties()
            if (rootProject.file("github.properties").exists()) {
                githubProperties.load(new FileInputStream(rootProject.file("github.properties")))
            }

            name = "GitHubPackages"
            /*  Configure path to the library hosted on GitHub Package Registry
             *  Replace UserID with package owner userID and REPOSITORY with the repository name
             *  e.g. ""https://maven.pkg.github.com/opensrp/opensrp-client-reporting""
             */
            url = uri("https://maven.pkg.github.com/onaio/kujaku")
            credentials {
                username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
            }

        }
        maven { url 'https://maven.fabric.io/public' }
        mavenLocal()
    }
}


apply plugin: 'com.android.application'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply from: "jacoco.gradle"
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

jacoco {
    toolVersion = "0.8.5"
}

android {
    useLibrary 'org.apache.http.legacy'
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    defaultConfig {
        // Required when setting minSdkVersion to 20 or lower
        multiDexEnabled true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "org.smartregister.chw"
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode 1
        versionName "0.2.0"
        multiDexEnabled true
        buildConfigField "long", "MAX_SERVER_TIME_DIFFERENCE", "1800000l"
        buildConfigField "boolean", "TIME_CHECK", "false"
        buildConfigField "String", "SYNC_TYPE", '"teamId"'
        buildConfigField "int", "MAX_SYNC_RETRIES", '3'
        buildConfigField "long", "BUILD_TIMESTAMP", System.currentTimeMillis() + "L"
        buildConfigField "boolean", "IS_SYNC_SETTINGS", "false"
        buildConfigField "String", "THINKMD_BASE_URL", '"https://app.africa.thinkmd.tech"'
        buildConfigField "String", "THINKMD_END_POINT", '"/#/start"'
        buildConfigField "int", "MAX_CONNECTION_TIMEOUT", '1'
        buildConfigField "int", "MAX_READ_TIMEOUT", '1'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }

        if (project.rootProject.file("local.properties").exists()) {

            Properties properties = new Properties()
            properties.load(project.rootProject.file("local.properties").newDataInputStream())

            if (properties != null && properties.containsKey("oauth.client.id")) {

                buildConfigField "String", "OAUTH_CLIENT_ID", properties["oauth.client.id"]

            } else {
                project.logger.error("oauth.client.id variable is not set in your local.properties")
                buildConfigField "String", "OAUTH_CLIENT_ID", "\"sample_client_id\""
            }


            if (properties != null && properties.containsKey("oauth.client.secret")) {

                buildConfigField "String", "OAUTH_CLIENT_SECRET", properties["oauth.client.secret"]

            } else {
                project.logger.error("oauth.client.secret variable is not set in your local.properties")
                buildConfigField "String", "OAUTH_CLIENT_SECRET", "\"sample_client_secret\""
            }

            if (properties != null &&
                    properties.containsKey("mapbox.sdk.token")) {
                buildConfigField "String", "MAPBOX_SDK_ACCESS_TOKEN", properties["mapbox.sdk.token"]
            } else {
                println("Mapbox key config variables is not set in your local.properties")
                buildConfigField "String", "MAPBOX_SDK_ACCESS_TOKEN", "\"sample_key\""
            }

        } else {
            println("local.properties does not exist")
            buildConfigField "String", "OAUTH_CLIENT_ID", "\"sample_client_id\""
            buildConfigField "String", "OAUTH_CLIENT_SECRET", "\"sample_client_secret\""
            buildConfigField "String", "MAPBOX_SDK_ACCESS_TOKEN", "\"sample_key\""
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    buildTypes {

        release {
            //debuggable true // Useful when debugging release builds
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "TIME_CHECK", "true"
            buildConfigField "int", "DATA_SYNC_DURATION_MINUTES", '15'
            buildConfigField "int", "VACCINE_SYNC_PROCESSING_MINUTES", '30'
            buildConfigField "int", "IMAGE_UPLOAD_MINUTES", '180'
            buildConfigField "int", "PULL_UNIQUE_IDS_MINUTES", '180'
            buildConfigField "int", "HOME_VISIT_MINUTES", '60'
            buildConfigField "int", "REPORT_INDICATOR_GENERATION_MINUTES", '120'
            buildConfigField "int", "BASE_PNC_CLOSE_MINUTES", '1440'
            buildConfigField "int", "SCHEDULE_SERVICE_MINUTES", '720'
            buildConfigField "int", "STOCK_USAGE_REPORT_MINUTES", '1440'
            buildConfigField "boolean", "USE_UNIFIED_REFERRAL_APPROACH", "true"
            buildConfigField "boolean", "BUILD_FOR_BORESHA_AFYA_SOUTH", "false"
            buildConfigField "boolean", "LOG_CRASHLYTICS", "true"
        }

        debug {
            buildConfigField "boolean", "TIME_CHECK", "false"
            buildConfigField "int", "DATA_SYNC_DURATION_MINUTES", '15'
            buildConfigField "int", "VACCINE_SYNC_PROCESSING_MINUTES", '30'
            buildConfigField "int", "IMAGE_UPLOAD_MINUTES", '15'
            buildConfigField "int", "PULL_UNIQUE_IDS_MINUTES", '15'
            buildConfigField "int", "HOME_VISIT_MINUTES", '60'
            buildConfigField "int", "REPORT_INDICATOR_GENERATION_MINUTES", '120'
            buildConfigField "int", "BASE_PNC_CLOSE_MINUTES", '1440'
            buildConfigField "int", "SCHEDULE_SERVICE_MINUTES", '360'
            buildConfigField "int", "STOCK_USAGE_REPORT_MINUTES", '1440'
            buildConfigField "boolean", "USE_UNIFIED_REFERRAL_APPROACH", "true"
            buildConfigField "boolean", "BUILD_FOR_BORESHA_AFYA_SOUTH", "false"
            buildConfigField "boolean", "LOG_CRASHLYTICS", "false"
            testCoverageEnabled true
        }

        preview {
            initWith debug
            buildConfigField "boolean", "LOG_CRASHLYTICS", "true"
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.md'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/NOTICE.md'
        exclude 'META-INF/INDEX.LIST'
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }

        unitTests.all {
            systemProperty 'robolectric.enabledSdks', '28'
            jvmArgs '-noverify'
        }
    }

    configurations.all {
        resolutionStrategy {
            force 'androidx.appcompat:appcompat:1.0.2'
            force 'com.google.android.material:material:1.0.0'
        }
    }

    flavorDimensions 'baseDimension'
    productFlavors {
        ba {
            dimension = 'baseDimension'
            applicationIdSuffix ".ba"
            versionCode 8
            versionName "1.0.8"
            buildConfigField "int", "OPENMRS_UNIQUE_ID_INITIAL_BATCH_SIZE", '10000'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_BATCH_SIZE", '10000'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_SOURCE", '2'
            buildConfigField "String", 'opensrp_url', '"https://boresha-afya.smartregister.org/opensrp/"'
            buildConfigField "String", 'guidebooks_url', '"https://opensrp.s3.amazonaws.com/media/ba/"'
            buildConfigField "String", 'opensrp_url_debug', '"https://boresha-afya-stage.smartregister.org/opensrp/"'
//            buildConfigField "String", 'opensrp_url_debug', '"https://ba-unified-stage.smartregister.org/opensrp/"'
            buildConfigField "String[]", "LOCATION_HIERACHY", '{"Country","Region","District","Ward" , "MOH Jhpiego Facility Name", "Health Facility", "Village", "Village Sublocations"}'
            buildConfigField "String[]", "ALLOWED_LOCATION_LEVELS", '{"Ward" , "MOH Jhpiego Facility Name", "Health Facility", "Village", "Village Sublocations"}'
            buildConfigField "String[]", "ALLOWED_LOCATION_LEVELS_DEBUG", '{"Ward", "MOH Jhpiego Facility Name", "Health Facility", "Village", "Village Sublocations"}'
            buildConfigField "String", 'DEFAULT_LOCATION', '"Village Sublocations"'
            buildConfigField "String", 'DEFAULT_LOCATION_DEBUG', '"Village"'
            buildConfigField "int", "DATABASE_VERSION", '23'
        }
        chad {
            dimension = 'baseDimension'
            applicationIdSuffix ".chad"
            versionCode 3
            versionName "1.0.3"
            buildConfigField "int", "OPENMRS_UNIQUE_ID_INITIAL_BATCH_SIZE", '10000'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_BATCH_SIZE", '10000'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_SOURCE", '2'
            buildConfigField "String", 'opensrp_url', '"https://wcaro-td.smartregister.org/opensrp/"'
            buildConfigField "String", 'guidebooks_url', '"https://opensrp.s3.amazonaws.com/media/chad/"'
            buildConfigField "String", 'opensrp_url_debug', '"https://wcaro-stage.smartregister.org/opensrp/"'
            buildConfigField "String[]", "LOCATION_HIERACHY", '{"Country" , "Province" , "Health District","Health Facility","Village","Clinic" , "CHSS" , "CHA"}'
            buildConfigField "String[]", "ALLOWED_LOCATION_LEVELS", '{"Health District","Health Facility","Village"}'
            buildConfigField "String[]", "ALLOWED_LOCATION_LEVELS_DEBUG", '{"Clinic" , "CHSS" , "CHA"}'
            buildConfigField "String", 'DEFAULT_LOCATION', '"Village"'
            buildConfigField "String", 'DEFAULT_LOCATION_DEBUG', '"CHA"'
            buildConfigField "int", "DATABASE_VERSION", '15'
            buildConfigField "int", "MAX_CONNECTION_TIMEOUT", '5'
            buildConfigField "int", "MAX_READ_TIMEOUT", '5'
        }
        drc {
            resConfigs "en", "fr"
            dimension = 'baseDimension'
            applicationIdSuffix ".drc"
            versionCode 11
            versionName "1.0.16"
            buildConfigField "int", "OPENMRS_UNIQUE_ID_INITIAL_BATCH_SIZE", '10000'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_BATCH_SIZE", '10000'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_SOURCE", '2'
            buildConfigField "String", 'opensrp_url', '"https://wcaro-cd.smartregister.org/opensrp/"'
            buildConfigField "String", 'guidebooks_url', '"https://opensrp.s3.amazonaws.com/media/drc/"'
            buildConfigField "String", 'opensrp_url_preview', '"https://wcaro-cd-preview.smartregister.org/opensrp/"'
            buildConfigField "String", 'opensrp_url_debug', '"https://wcaro-stage.smartregister.org/opensrp/"'
            buildConfigField "String[]", "LOCATION_HIERACHY", '{"Pays" , "Province(DPS)" , "Zone de Sante","Aire de Sante", "CAC" ,"VILLAGE/COMMUNAUTE"}'
            buildConfigField "String[]", "ALLOWED_LOCATION_LEVELS", '{"Pays" , "Province(DPS)" , "Zone de Sante","Aire de Sante" , "CAC" ,"VILLAGE/COMMUNAUTE"}'
            buildConfigField "String[]", "ALLOWED_LOCATION_LEVELS_DEBUG", '{"Pays" , "Province(DPS)" , "Zone de Sante","Aire de Sante" , "CAC" ,"VILLAGE/COMMUNAUTE"}'
            buildConfigField "String", 'DEFAULT_LOCATION_DEBUG', '"VILLAGE/COMMUNAUTE"'
            buildConfigField "String", 'DEFAULT_LOCATION', '"VILLAGE/COMMUNAUTE"'
            buildConfigField "int", "MAX_CONNECTION_TIMEOUT", '5'
            buildConfigField "int", "MAX_READ_TIMEOUT", '5'
            buildConfigField "int", "DATABASE_VERSION", '12'
        }
        guinea {
            dimension = 'baseDimension'
            applicationIdSuffix ".guinea"
            versionCode 4
            versionName "1.0.3"
            buildConfigField "int", "OPENMRS_UNIQUE_ID_SOURCE", '2'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_INITIAL_BATCH_SIZE", '1000'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_BATCH_SIZE", '500'
            buildConfigField "String", 'opensrp_url', '"https://wcaro-guinea.smartregister.org/opensrp/"'
            buildConfigField "String", 'guidebooks_url', '"https://opensrp.s3.amazonaws.com/media/guinea/"'
            buildConfigField "String", 'opensrp_url_preview', '"https://wcaro-guinea-preview.smartregister.org/opensrp/"'
            buildConfigField "String", 'opensrp_url_debug', '"https://wcaro-stage.smartregister.org/opensrp/"'
            buildConfigField "String[]", "LOCATION_HIERACHY", '{"Country" , "Region" , "Prefecture" , "Commune" , "Health Facility" , "Village"}'
            buildConfigField "String[]", "ALLOWED_LOCATION_LEVELS_PREVIEW", '{"Health Facility" , "Village"}'
            buildConfigField "String[]", "ALLOWED_LOCATION_LEVELS_DEBUG", '{"Country", "Clinic" , "CHSS" , "CHA", "Supervisor", "CHW"}'
            buildConfigField "String[]", "ALLOWED_LOCATION_LEVELS", '{"Health Facility" , "Village"}'
            buildConfigField "String", 'DEFAULT_LOCATION_PREVIEW', '"Village"'
            buildConfigField "String", 'DEFAULT_LOCATION_DEBUG', '"CHW"'
            buildConfigField "String", 'DEFAULT_LOCATION', '"Village"'
            buildConfigField "int", "DATABASE_VERSION", '8'
        }
        togo {
            dimension = 'baseDimension'
            applicationIdSuffix ".togo"
            versionCode 26
            versionName "1.2.7"
            buildConfigField "int", "OPENMRS_UNIQUE_ID_SOURCE", '2'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_INITIAL_BATCH_SIZE", '1000'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_BATCH_SIZE", '500'
            buildConfigField "String", 'opensrp_url', '"https://wcaro-tg.smartregister.org/opensrp/"'
            buildConfigField "String", 'guidebooks_url', '"https://opensrp.s3.amazonaws.com/media/togo/"'
            buildConfigField "String", 'opensrp_url_debug', '"https://wcaro-stage.smartregister.org/opensrp/"'
            buildConfigField "String[]", "LOCATION_HIERACHY", '{"National", "Regional" , "District" , "Formation sanitaire", "Supervisor", "Village" }'
            buildConfigField "String[]", "ALLOWED_LOCATION_LEVELS_DEBUG", '{"National", "Regional" , "District" , "Formation sanitaire", "Supervisor", "Village"}'
            buildConfigField "String[]", "ALLOWED_LOCATION_LEVELS", '{"National", "Regional" , "District" , "Formation sanitaire", "Supervisor", "Village"}'
            buildConfigField "String", 'DEFAULT_LOCATION_DEBUG', '"Village"'
            buildConfigField "String", 'DEFAULT_LOCATION', '"Village"'
            buildConfigField "int", "MAX_CONNECTION_TIMEOUT", '10'
            buildConfigField "int", "MAX_READ_TIMEOUT", '10'
            buildConfigField "int", "DATABASE_VERSION", '21'
        }
        liberia {
            dimension = 'baseDimension'
            versionCode 8
            versionName "1.0.1"
            buildConfigField "int", "OPENMRS_UNIQUE_ID_INITIAL_BATCH_SIZE", '10000'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_BATCH_SIZE", '10000'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_SOURCE", '2'
            buildConfigField "String", 'opensrp_url', '"https://wcaro-lr.smartregister.org/opensrp/"'
            buildConfigField "String", 'guidebooks_url', '"https://opensrp.s3.amazonaws.com/media/liberia/"'
            buildConfigField "String", 'opensrp_url_debug', '"https://wcaro-stage.smartregister.org/opensrp/"'
            buildConfigField "String[]", "LOCATION_HIERACHY", '{"Clinic" , "CHSS" , "CHA"}'
            buildConfigField "String[]", "ALLOWED_LOCATION_LEVELS", '{"Country" , "County" , "District", "Clinics", "CHSS", "Village"}'
            buildConfigField "String[]", "ALLOWED_LOCATION_LEVELS_DEBUG", '{"Clinic" , "CHSS" , "CHA"}'
            buildConfigField "String", 'DEFAULT_LOCATION', '"Village"'
            buildConfigField "String", 'DEFAULT_LOCATION_DEBUG', '"CHA"'
            buildConfigField "int", "MAX_CONNECTION_TIMEOUT", '5'
            buildConfigField "int", "MAX_READ_TIMEOUT", '5'
            buildConfigField "int", "DATABASE_VERSION", '8'
        }
        lmh {
            dimension = 'baseDimension'
            applicationIdSuffix ".lmh"
            versionCode 7
            versionName "2.0.3"
            buildConfigField "int", "OPENMRS_UNIQUE_ID_INITIAL_BATCH_SIZE", '10000'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_BATCH_SIZE", '10000'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_SOURCE", '1'
            buildConfigField "String", 'opensrp_url', '"https://liberiavts.smartregister.org/opensrp/"'
            buildConfigField "String", 'guidebooks_url', '"https://opensrp.s3.amazonaws.com/media/lmh/"'
            buildConfigField "String", 'opensrp_url_debug', '"https://lmh-liberia-preview.smartregister.org/opensrp/"'
            buildConfigField "String[]", "LOCATION_HIERACHY", '{"Country" , "County" , "District", "Clinics", "CHSS", "Village","Commune"}'
            buildConfigField "String[]", "ALLOWED_LOCATION_LEVELS", '{"Country" , "County" , "District", "Clinics", "CHSS", "Village","Commune"}'
            buildConfigField "String[]", "ALLOWED_LOCATION_LEVELS_DEBUG", '{"Country" , "County" , "District", "Clinics", "CHSS", "Village","Commune"}'
            buildConfigField "String", 'DEFAULT_LOCATION', '"Commune"'
            buildConfigField "String", 'DEFAULT_LOCATION_DEBUG', '"Commune"'
            buildConfigField "int", "DATABASE_VERSION", '7'
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation('com.google.android.gms:play-services-vision:17.0.2')

    implementation('org.smartregister:opensrp-client-chw-core:2.1.4-SNAPSHOT@aar') {
        transitive = true
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'androidx.legacy', module: 'legacy-support-v4'
        exclude group: 'androidx.appcompat', module: 'appcompat'
        exclude group: 'androidx.constraintlayout', module: 'constraintlayout'
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.rengwuxian.materialedittext', module: 'library'
        exclude group: 'stax', module: 'stax-api'
    }

    implementation 'com.mapbox.mapboxsdk:mapbox-sdk-turf:5.1.0'
    implementation('io.ona.kujaku:library:0.9.0') {
        exclude group: 'com.android.volley', module: 'volley'
        exclude group: 'stax', module: 'stax-api'
    }

    //Do not upgrade to 1.1.0 due to compatibility issues
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    //Do not upgrade to 1.1.0 due to compatibility issues
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.android.support:multidex:1.0.3'
    //Do not upgrade to 24.jre-1 due to compatibility issues
    implementation 'com.google.guava:guava:24.1-jre'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'org.mockito:mockito-android:3.0.0'

    //implementation 'com.github.lecho:hellocharts-android:1.5.8@aar'
    //For viewing PDFs in the app
    implementation 'com.github.barteksc:android-pdf-viewer:3.2.0-beta.1'

    implementation 'com.github.lecho:hellocharts-android:1.5.8@aar'

    testImplementation "org.koin:koin-test:2.0.1"
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.mockito:mockito-core:3.1.0'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'org.robolectric:shadows-multidex:4.3.1'
    testImplementation 'org.robolectric:shadows-support-v4:3.4-rc2'
    testImplementation 'androidx.test:core:1.3.0'
    //  testImplementation 'com.ibm.fhir:fhir-model:4.7.0'
    // testImplementation 'com.ibm.fhir:fhir-path:4.7.0'

    // unit test
    def powerMockVersion = '2.0.7'
    testImplementation "org.powermock:powermock-module-junit4:$powerMockVersion"
    testImplementation "org.powermock:powermock-module-junit4-rule:$powerMockVersion"
    testImplementation "org.powermock:powermock-api-mockito2:$powerMockVersion"
    testImplementation "org.powermock:powermock-classloading-xstream:$powerMockVersion"
}

def flavors = android.productFlavors.collect { flavor -> flavor.name }
if (!flavors) flavors.add('')

def testDependencies = []
flavors.each { productFlavorName ->
    testDependencies.add("test${productFlavorName.capitalize()}DebugUnitTest")
    testDependencies.add("create${productFlavorName.capitalize()}DebugCoverageReport")
}

// task jacocoTestReport(type: JacocoReport, dependsOn: testDependencies.take(2)) {
task jacocoTestReport(type: JacocoReport, dependsOn: [
        'testTogoDebugUnitTest', 'createTogoDebugCoverageReport',
        'testBaDebugUnitTest', 'createBaDebugCoverageReport'
]) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    getReports().getXml().setDestination(file("${buildDir}/reports/jacoco/jacocoRootReport/merged.xml"))
    getReports().getHtml().setDestination(file("${buildDir}/reports/jacoco/jacocoRootReport/html"))

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', '**/*$ViewBinder*.*']
    def debugTree = fileTree(dir: "$project.buildDir/intermediates/javac/baDebug/classes/", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(fileTree(dir: project.buildDir, includes: [
            'jacoco/testBaDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ]))
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    maxHeapSize = "3g"

    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

tasks.coveralls {
    dependsOn 'jacocoTestReport'
}

coveralls {
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/merged.xml"
    sourceDirs = ["$project.projectDir/src/main/java"]
}